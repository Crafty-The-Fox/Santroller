cmake_minimum_required(VERSION 3.6)
project(foo)
file(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/firmware)
# For AVR, we have projects for arduinos that are similar enough to run the same
# code after that there are variants that specify small differences, such as the
# micro vs leo
set(PROJECTS "micro;uno")
set(OUTPUT_EXT "bin;eep;elf;hex;lss;map;sym")

set(uno_PID 0x0001)
set(mega2560_PID 0x0010)
set(megaadk_PID 0x003f)

set(uno_main_MCUS atmega328p)
set(mega2560_main_MCUS atmega2560)
set(megaadk_main_MCUS atmega2560)
set(mini_main_MCUS atmega328p)

set(uno_usb_MCUS "atmega16u2;at90usb82")
set(mega2560_usb_MCUS "atmega16u2;at90usb82")
set(megaadk_usb_MCUS "atmega16u2;at90usb82")

set(micro_main_MCUS atmega32u4)
set(a-micro_main_MCUS atmega32u4)
set(leonardo_main_MCUS atmega32u4)

set(micro_VARIANT micro)
set(a-micro_VARIANT micro)
set(leonardo_VARIANT micro)

set(mini_VARIANT uno)
set(uno_VARIANT uno)
set(mega2560_VARIANT mega)
set(megaadk_VARIANT mega)

set(uno_VARIANTS "uno;mega2560;megaadk;mini")
set(micro_VARIANTS "micro;a-micro;leonardo")
set(F_CPU_8_mini TRUE)
set(F_CPU_8_micro TRUE)
foreach(PROJECT ${PROJECTS})
  foreach(VARIANT ${${PROJECT}_VARIANTS})
    set(TYPES "rf;multi;main")
    if(${PROJECT} MATCHES "uno")
      list(APPEND TYPES usb)
      list(APPEND TYPES original-usb)
    endif()
    # Minis only support RF, as they don't have usb
    if(${VARIANT} MATCHES "mini")
      set(TYPES rf)
    endif()
    foreach(TYPE ${TYPES})
      set(F_CPUS 16000000)
      if(${F_CPU_8_${PROJECT}})
        list(APPEND F_CPUS 8000000)
      endif()
      set(MCU_LOOKUP ${TYPE})
      if(NOT ${TYPE} MATCHES ".*usb")
        set(MCU_LOOKUP main)
      else()
        set(MCU_LOOKUP usb)
      endif()
      foreach(MCU ${${VARIANT}_${MCU_LOOKUP}_MCUS})
        foreach(F_CPU ${F_CPUS})
          unset(EXTRA)
          set(BUILD_TYPE ${TYPE})
          if(NOT ${TYPE} MATCHES "usb")
            set(BUILD_TYPE main)
            if(NOT ${TYPE} MATCHES "main")
              set(EXTRA -${TYPE})
            endif()
          else()
            set(BUILD_TYPE usb)
            if(${TYPE} MATCHES "original-usb")
              set(EXTRA -usbserial)
            endif()
          endif()
          set(PID ${${VARIANT}_PID})
          set(TARGET ardwiino-${VARIANT}-${BUILD_TYPE}-${MCU}-${F_CPU}${EXTRA})
          set(IN ../src/avr/${PROJECT}/${BUILD_TYPE})
          set(OUT ${CMAKE_CURRENT_BINARY_DIR}/firmware/${TARGET})
          add_custom_target(${TARGET} ALL)
          unset(OUTPUTS)
          foreach(OUTPUT ${OUTPUT_EXT})
            list(APPEND OUTPUTS ${OUT}.${OUTPUT})
          endforeach()
          add_custom_command(
            TARGET ${TARGET}
            COMMAND
              make OBJDIR=obj/${TARGET} F_USB=${F_CPU} F_CPU=${F_CPU}
              ARDUINO_MODEL_PID=${PID} ARDWIINO_BOARD=${VARIANT} EXTRA=${EXTRA}
              TARGET=${OUT} MCU=${MCU} VARIANT=${${VARIANT}_VARIANT}
            WORKING_DIRECTORY ${IN}
            BYPRODUCTS ${IN}/obj/${TARGET} ${OUTPUTS})
          message(STATUS ${IN})
          message(STATUS ${OUT})
        endforeach()
      endforeach()
    endforeach()
  endforeach()
endforeach()
